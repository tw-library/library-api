group 'com.thoughtworks.librarysystem'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'spring-boot'

ext {
    expectedLineCoverage = 50
    expectedBranchCoverage = 60
}

jar {
    baseName = 'librarysystem-api'
    version = '0.1.0'
}

loadConfiguration()

def loadConfiguration() {
    def environment = hasProperty('env') ? env : System.getenv('APP_ENV')
    environment = environment != null ? environment : 'development'

    project.ext.environment = environment
    println ">>>>>>> Environment is set to $environment"
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.4.RELEASE")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }

    compile("org.projectlombok:lombok:1.16.4")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-test")
    compile("org.hibernate:hibernate-core:4.3.6.Final")
    compile("org.hsqldb:hsqldb:2.0.0")
    compile("org.postgresql:postgresql:9.3-1100-jdbc4")

    compile("com.fasterxml.jackson.core:jackson-core:2.4.1")
    compile("com.fasterxml.jackson.core:jackson-databind:2.4.1.1")

    testCompile 'com.jayway.restassured:rest-assured:2.5.0'
    testCompile 'com.jayway.restassured:spring-mock-mvc:2.5.0'

    testCompile group: "junit", name: "junit", version: "4.11"
    testCompile "junit:junit:4.11"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "com.h2database:h2:1.4.182"
    testCompile "javax.servlet:javax.servlet-api:3.0.1"
    testCompile "com.jayway.jsonpath:json-path:0.8.1"
    testCompile "com.jayway.jsonpath:json-path-assert:0.8.1"
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "javax.el:javax.el-api:3.0.0"
    testCompile "org.apache.velocity:velocity:1.6.2"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task copyConfiguration << {
    println "Target environment: $environment"

    copy {
        from "src/main/environment/$environment"
        into "src/main/resources"
        include "*/"
    }
}
processResources.dependsOn copyConfiguration

task stage(dependsOn: ['clean', 'installApp'])

task checkTestCoverage << {
    def coverageReportPath = "build/reports/jacoco/test/html/index.html"
    def coverageFile = new File(coverageReportPath)
    def matcher = ( coverageFile.text =~ /<tfoot>(.*)<\/tfoot>/ )
    def coverageString = new XmlParser().parseText(matcher[0][0])

    def lineCoverageString = coverageString.tr.td[2].text()
    def branchCoverageString = coverageString.tr.td[4].text()
    def actualLineCoverage=  lineCoverageString.substring(0,lineCoverageString.length() - 1).toInteger()
    def actualBranchCoverage=  branchCoverageString.substring(0,branchCoverageString.length() - 1).toInteger()

    logger.quiet('\n\nCode line coverage is ' +actualLineCoverage+'%')
    logger.quiet('Code branch coverage is ' +actualBranchCoverage+'%')
    logger.quiet('Form more info check '+coverageReportPath+'\n\n')
}

checkTestCoverage.dependsOn jacocoTestReport

test.finalizedBy checkTestCoverage